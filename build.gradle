buildscript {
	repositories { jcenter() }
	dependencies {
		classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.8'
		classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
	}
}

subprojects {
	apply plugin: 'org.asciidoctor.convert'

	task runDoc(type: org.asciidoctor.gradle.AsciidoctorTask) {
		description = 'Runs the asciidoctor task and renames the the output article file'
		group = 'Documentation'

		backends = ['html5', 'pdf']
		sourceDir file(projectDir)
		sources { include '001_article.adoc' }
		resources {
			from(sourceDir) { include 'img/**' }
		}
		outputDir "${buildDir}/"

		attributes	'source-highlighter' : 'coderay',
					'toc':'left',
					'icons': 'font',
					'setanchors':'true',
					'idprefix':'',
					'idseparator':'-',
					'docinfo1':'true'

		doLast {
			def dir = new File("${buildDir}")
			dir.eachFileRecurse( {
				if(it.name.startsWith("001_article")){
					println it
					it.renameTo(new File(it.parent, it.name.substring(4, it.name.length())))
				}
			}
			)
		}
	}
}
